pkgname=(voidedskel sysmon corescripts enckse-extras labsite)
_pkgname=coretools
_home=home
pkgver=2020.1513.3165
pkgrel=1
url="https://cgit.voidedtech.com/coretools/"
arch=('x86_64')
license=('MIT')
_giturl="git+git://cgit.voidedtech.com/"
makedepends=("git" "go" "go-bindata" "golint")
source=("$_pkgname::${_giturl}$_pkgname"
        "$_home::${_giturl}$_home")
md5sums=('SKIP' 'SKIP')

pkgver(){
    echo $(date +%Y).$(git -C $_pkgname rev-list --count master).$(git -C $_home rev-list --count master)
}

build() {
    cd $_pkgname
    cd server
    make package
    cd ../labsite
    make labsite
    cd ../common/tools
    make qrenc md2slides
}

package_corescripts() {
    pkgdesc="scripts for core systems"
    depends=('rclone' 'rsync' 'isync' 'sysmon' 'nginx')
    local f
    cd $_pkgname/server
    install -d $pkgdir/usr/lib/systemd/system
    install -d $pkgdir/usr/bin
    install -d $pkgdir/usr/lib/tmpfiles.d/
    install -d $pkgdir/usr/lib/core-scripts/
    for f in $(find . -type f | grep -E "service|path|timer"); do
      install -Dm644 $f $pkgdir/usr/lib/systemd/system/$(basename $f)
    done
    for f in $(ls scripts); do
      install -Dm644 scripts/$f $pkgdir/usr/lib/core-scripts/$f
    done
    for f in definitions corescripts.pm; do
      install -Dm644 bin/$f $pkgdir/usr/lib/core-scripts/$f
    done
    install -Dm755 bin/core-scripts $pkgdir/usr/bin/
    install -Dm644 bin/core-scripts.conf $pkgdir/usr/lib/tmpfiles.d/
}

package_sysmon() {
    depends=('git' 'nftables' 'xfsprogs' 'voidedskel')
    pkgdesc="system monitoring"
    local f
    cd $_pkgname/common/sysmon
    install -d $pkgdir/usr/lib/systemd/system
    install -d $pkgdir/usr/lib/sysmon
    install -d $pkgdir/usr/bin
    for f in timer service; do
        install -Dm644 systemd/sysmon.$f $pkgdir/usr/lib/systemd/system/sysmon.$f
    done
    install -Dm755 sysmon.pl $pkgdir/usr/bin/sysmon
    for f in motd pack; do
        install -Dm644 $f.pl $pkgdir/usr/lib/sysmon/$f.pl
    done
}

package_voidedskel() {
    depends=('ripgrep' 'bash' 'vimsym' 'vim-airline' 'fzf' 'vim-fzf-git')
    pkgdesc="skeleton files for voidedtech"
    cd $_pkgname/common/
    local f s
    for s in skel bash; do
        install -d $pkgdir/etc/voidedtech/$s
        for f in $(ls $s); do
            install -Dm644 $s/$f $pkgdir/etc/voidedtech/$s/$(basename $f)
        done
    done
    install -Dm644 ../../$_home/.vimrc $pkgdir/etc/voidedtech/skel/vimrc
}

package_enckse-extras() {
    pkgdesc="tools for extended use"
    depends=("poppler" "wkhtmltopdf" "python-pygments" "python-markdown")
    cd $_pkgname/common/tools/
    install -d $pkgdir/usr/bin/
    install -Dm755 bin/qrenc $pkgdir/usr/bin/
    install -Dm755 bin/md2slides $pkgdir/usr/bin/
}

package_labsite() {
    cd $_pkgname/labsite
    install -Dm755 bin/labsite $pkgdir/usr/bin/labsite
    install -Dm644 completions/labsite $pkgdir/usr/share/bash-completion/completions/labsite
}
