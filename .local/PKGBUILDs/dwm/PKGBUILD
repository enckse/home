pkgname=dwm-git
_pkgname=dwm
_majorminor=6.2
pkgver=6.2.1700
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="https://cgit.voidedtech.com/external/dwm/"
arch=('x86_64')
license=('MIT')
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'dmenu')
makedepends=('git')
provides=('dwm')
conflicts=('dwm')
source=("$_pkgname::git+git://cgit.voidedtech.com/external/dwm")
md5sums=('SKIP')

pkgver(){
    cd $_pkgname
    echo $_majorminor.$(git rev-list --count enckse)
}

build() {
    cd $_pkgname
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd $_pkgname
    make PREFIX=/usr DESTDIR="$pkgdir" install
}
